plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlinx-serialization'
    id 'com.google.devtools.ksp' version '1.9.10-1.0.13'
    id 'kotlin-kapt'
}

android {
    namespace 'com.st.robotics'
    compileSdk rootProject.compileSdk

    defaultConfig {
        applicationId "com.st.robotics"
        minSdk rootProject.minSdk
        targetSdk rootProject.targetSdk
        versionCode 3
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        buildConfigField "String", "BLUESTSDK_DB_BASE_URL", "\"https://raw.githubusercontent.com/STMicroelectronics/appconfig/blesensor_5.2.0/bluestsdkv2/\""
    }

    flavorDimensions += "env"

    productFlavors {
        prod {
            dimension "env"
            buildConfigField "String", "ENVIRONMENT", "\"PROD\""
            buildConfigField "String", "HELP_URL", "\"https://staiotcraft.st.com/#/wiki?id=2_3_mobile_app.md\""
            buildConfigField "String", "SECRETS_BASE_URL", "\"https://staiotcraft.st.com/svc/secretstore/v1alpha1/\""
            buildConfigField "String", "DATASET_BASE_URL", "\"https://staiotcraft.st.com/svc/data/v1alpha1/\""
            buildConfigField "String", "BASE_URL", "\"https://staiotcraft.st.com/svc/main-rest-api/3/\""
            buildConfigField "String", "CUSTOM_LOGOUT_URL", "\"https://www.st.com/cas/logout?service=https%3A%2F%2Fstaiotcraft.st.com%2Fsvc%2Fwebtomobile%2Fstvespucci\""
            buildConfigField "String", "VESPUCCI_CALLBACK", "\"stvespucci://callback\""
            buildConfigField "String", "APP_CENTER_API_KEY", "\"22224b69-9a65-40f3-be88-d869ef0c1cfc\""
            buildConfigField "String", "TRACKING_BASE_URL", "\"https://staiotcraft.st.com/svc/tracking-api/v1alpha1/\""
        }

        preProd {
            dimension "env"
            versionNameSuffix "-pre-prod"
            applicationId "com.st.staiotcraft_pre_prod"
            buildConfigField "String", "ENVIRONMENT", "\"PRE_PROD\""
            buildConfigField "String", "HELP_URL", "\"https://qa.stm-vespucci.com/#/wiki?id=2_3_mobile_app.md\""
            buildConfigField "String", "SECRETS_BASE_URL", "\"https://qa.stm-vespucci.com/svc/secretstore/v1alpha1/\""
            buildConfigField "String", "DATASET_BASE_URL", "\"https://qa.vespucci.st.com/svc/data/v1alpha1/\""
            buildConfigField "String", "BASE_URL", "\"https://qa.stm-vespucci.com/svc/main-rest-api/3/\""
            buildConfigField "String", "CUSTOM_LOGOUT_URL", "\"https://qa-www2.st.com/cas/logout?service=https%3A%2F%2Fvespucci.st.com%2Fsvc%2Fwebtomobile%2Fstvespucci\""
            buildConfigField "String", "VESPUCCI_CALLBACK", "\"stvespucci://callback\""
            buildConfigField "String", "APP_CENTER_API_KEY", "\"f871d8b5-d131-46e5-bf52-dab5ce348d6e\""
            buildConfigField "String", "TRACKING_BASE_URL", "\"https://dev.stm-vespucci.com/svc/tracking-api/v1alpha1/\""
        }

        dev {
            dimension "env"
            versionNameSuffix "-dev"
            applicationId "com.st.staiotcraft_dev"
            buildConfigField "String", "ENVIRONMENT", "\"DEV\""
            buildConfigField "String", "HELP_URL", "\"https://dev.stm-vespucci.com/#/wiki?id=2_3_mobile_app.md\""
            buildConfigField "String", "SECRETS_BASE_URL", "\"https://dev.stm-vespucci.com/svc/secretstore/v1alpha1/\""
            buildConfigField "String", "DATASET_BASE_URL", "\"https://dev.stm-vespucci.com/svc/data/v1alpha1/\""
            buildConfigField "String", "BASE_URL", "\"https://dev.stm-vespucci.com/svc/main-rest-api/3/\""
            buildConfigField "String", "VESPUCCI_CALLBACK", "\"stvespucci://callback\""
            buildConfigField "String", "CUSTOM_LOGOUT_URL", "\"\""
            buildConfigField "String", "APP_CENTER_API_KEY", "\"83664b13-3fbf-4ea6-82f9-a78bf721f044\""
            buildConfigField "String", "TRACKING_BASE_URL", "\"https://dev.stm-vespucci.com/svc/tracking-api/v1alpha1/\""
        }
    }

    hilt {
        enableAggregatingTask = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        compose true
        viewBinding true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Blue ST SDK
    implementation (libs.st.blue.sdk)

    //network
    implementation libs.bundles.network
    implementation libs.okhttp
    implementation libs.logging.interceptor
    implementation libs.play.services.code.scanner
    testImplementation libs.junit.junit
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugaring_version"
    implementation 'com.auth0.android:jwtdecode:2.0.2'
    implementation 'com.auth0:java-jwt:4.5.0'

    // Compose
    def composeBom = platform("androidx.compose:compose-bom:$compose_bom_date")
    implementation composeBom
    androidTestImplementation composeBom

    // Material Design 3
    implementation libs.material3

    // or Material Design 2
    implementation libs.androidx.material
    implementation libs.ui

    // Android Studio Preview support
    implementation libs.ui.tooling.preview
    debugImplementation libs.ui.tooling

    // UI Tests
    androidTestImplementation libs.ui.test.junit4
    debugImplementation libs.ui.test.manifest

    // Optional - Add full set of material icons
    implementation libs.androidx.material.icons.extended
    // Optional - Add window size utils
    implementation "androidx.compose.material3:material3-window-size-class"
    // LiveData
    implementation(libs.androidx.lifecycle.livedata.ktx)

    // Integration with activities
    implementation libs.androidx.activity.compose.v182
    //Integration with ViewModels
    implementation libs.androidx.lifecycle.viewmodel.compose
    implementation libs.androidx.lifecycle.runtime.compose
    //Integration with LiveData
    implementation(libs.androidx.runtime.livedata)
    //Integration with Hilt
    implementation libs.androidx.hilt.navigation.compose
    //Integration with Navigation
    implementation libs.androidx.navigation.compose
    //Integration with ViewBinding
    implementation libs.androidx.ui.viewbinding

    // Core
    implementation libs.androidx.core.ktx.v1120
    implementation libs.material
    implementation libs.androidx.lifecycle.runtime.ktx.v270
    implementation libs.androidx.appcompat

    // Hilt
    implementation libs.androidx.hilt.navigation.fragment
    implementation libs.hilt.android
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Kotlinx serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization_version"
    implementation libs.retrofit2.kotlinx.serialization.converter

    // Accompanist
    implementation libs.accompanist.permissions

    // Room
    implementation libs.androidx.room.runtime
    ksp "androidx.room:room-compiler:$room_version"
    annotationProcessor("androidx.room:room-compiler:$room_version")
    implementation(libs.androidx.room.ktx)

    //animation
    implementation libs.androidx.animation

    //for plot charts
    implementation libs.ycharts

    //for splash screen
    implementation(libs.core.splashscreen)

    //coil
    implementation(libs.bundles.coil)

}